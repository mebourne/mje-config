#eval compdef -n _parse_opts latest
# ZSH function file
# View the latest of a set of files, given part of the filename
#
# $Id: latest,v 1.5 2002/11/20 16:16:45 mebourne Exp $

local -A opts
parse_opts - opts -- "$argv[@]" <<'EOF' || return 1
Description:
View the latest of a set of files, given part of the filename

Usage:
latest [options] <filename-stem>

Options:
  -h, --help			Provide this help
				# --help | -h * excludes=*

Arguments:
  <filename-stem>		Start of filename of set of files to view.
				May contain wildcards
				# filename-stem : ? file
EOF

# Get list of files sorted from latest to earliest modification time
local -a files
files=($~opts[filename-stem]*(Nom))

if (( $#files ))
then
  # Found at least one file. Either send it to pager or cat, depending on
  # whether stdout is a tty or not
  if [[ -t 1 ]]
  then
    $READNULLCMD $files[1]
  else
    cat $files[1]
  fi
else
  echo "File not found" 1>&2
  return 1
fi
