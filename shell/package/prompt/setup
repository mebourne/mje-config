# ZSH configuration file
# Executed when the prompt package is included
# Setup for prompt package
# Written by Martin Ebourne
#

add_functions_directory package/prompt/functions

# Declare work variables
typeset -g _prompt_pwd="" _prompt_justify="" _prompt_wrapmode=0 _prompt_retcode=0

# Cache terminal handling of line wrapping
if [[ $termcap[am] != yes || $termcap[xn] == yes ]]
then
  _prompt_wrapmode=1
fi

# Register our hooks
add_to_hook precmd _prompt_precmd
add_to_hook preexec '_prompt_preexec "$argv[1]"'

# Set _prompt_retcode as the first thing in precmd - else its value is lost
if [[ $functions[precmd] != *_prompt_retcode* ]]
then
  functions[precmd]="_prompt_retcode=\$?
$functions[precmd]
"
fi

# Remap ZLE clear-screen widget (Ctrl-L) to a modified version
zle -N clear-screen _prompt_clear_screen


## Now we configure the default prompts

typeset -g prompt_above="" prompt_right="" prompt_eol=""

# Configure the above line prompt
colour_setvar prompt_above
colour_addvar [dark_red] '%$COLUMNS>>%n' [grey] '@' [dark_green] '%m${ZHOSTALIAS:+,}${ZHOSTALIAS}'
if (( SHLVL > 1 ))
then
  colour_addvar [grey] '  #$[SHLVL-1]'
fi
colour_addvar [dark_red] '${${_prompt_retcode:#0}:+  [$_prompt_retcode${signals[$_prompt_retcode-127]:+ $signals[$_prompt_retcode-127]}]}'
colour_addvar [dark_red] '${jobstates:+  ${(AA)_prompt_jobs::=}${${(e)${(@j:, :)${(@)jobstates%%:*}/(#b)(?*)/_prompt_jobs[$match[1]]++}/(#b)(?*)/\$(($match[1]))}:#*}${(@kv)_prompt_jobs:/(#b)(???)[a-z]*/$match[1]}}'
colour_addvar [dark_magenta] '${_sql_prompt:+  $_sql_prompt}'
colour_addvar [dark_yellow] '$(_prompt_scminfo)'
colour_addvar [dark_cyan] '  $_prompt_pwd'
colour_addvar '$_prompt_justify'
colour_addvar [grey] '  %D{%a %f %b %Y %H:%M:%S}' [white]

# Configure the left hand prompt
colour_setvar PROMPT [bold] '%# ' [normal]

# Configure our special right hand prompt which is printed when the command has been entered
colour_setvar prompt_right [grey] '%D{%a %f %b %Y %H:%M:%S}' [white]

# Disable the standard ZSH right hand prompt
colour_setvar RPROMPT

# Configure the secondary prompt
colour_setvar PROMPT2 [bold] '%_> ' [normal]

# Configure the prompt used for execution tracing (set -x). Includes timestamp & seconds for
# profiling
colour_setvar PROMPT4
colour_addvar [grey] ': ' [dark_green] '%N' [grey] ':' [dark_yellow] '%i'
colour_addvar [grey] ' %D{%H:%M:%S} {$SECONDS} ; ' [white]

# Configure our end of line marker string
colour_setvar prompt_eol [red] '$' [white]
