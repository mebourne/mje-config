#eval compdef -n _parse_opts cfgupdate
# ZSH function file
# Written by Martin Ebourne
# Part of the Config package
# Update config directory
#
# $Id: cfgupdate 792 2003-09-22 11:47:18Z martin $

emulate -LR zsh

local base_dir="" file_desc="" remove_pattern="" ignore_pattern=""
local remove_derived="" compile_command=""

local -A opts
parse_opts - opts -- "$argv[@]" <<'EOF' || return 1
Description:
Update config directory.

Usage:
cfgupdate [options] <directory>

Options:
  -h, --help			Provide this help
				# --help | -h * excludes=*
  -n, --no-compile		Do not recompile directory
				# --no-compile | -n

Arguments:
  <directory>			The directory to update
				# directory : ? directory
EOF

# If path given then use it, else use default base dir
if [[ $opts[directory] == */* ]]
then
  base_dir=$opts[directory]
else
  base_dir=$config_dir/$opts[directory]
fi

case $opts[directory] in
  *shell) # This is the default
    file_desc="ZSH configuration system"
    remove_pattern='*~|\#*\#'
    ignore_pattern='*.zwc'
    remove_derived='$file.zwc'
    compile_command='cfg_compile_shell $base_dir'
    ;;

  *emacs)
    file_desc="Emacs configuration system"
    remove_pattern='*~|\#*\#'
    ignore_pattern='*.elc|loaddefs.el|startup.el'
    # Remove corresponding elc files
    remove_derived='${file}c'
    compile_command='cfg_compile_emacs $base_dir'
    ;;

  *perl)
    file_desc="Perl configuration system"
    remove_pattern='*~|\#*\#'
    ignore_pattern=''
    # Remove corresponding elc files
    remove_derived=''
    compile_command=''
    ;;

  *)
    echo "Syntax: cfgupdate <directory>" 1>&2
    echo "<directory> must be one of shell, emacs, perl (with optional path)" 1>&2
    return 1
    ;;
esac

echo "Updating directory $base_dir..."
if [[ ! -d $base_dir ]]
then
  echo "Error: No such directory" 1>&2
  return 1
fi

local -a remove_files non_vc_files changed_files unchanged_files

# Iterate through the RCS controlled directories in config
local dir="" file="" leaf="" name=""
local -a writeable
for dir in $base_dir/**/RCS(/)
do
  dir=${dir:h}

  # Check files currently here
  for file in $dir/*(.N)
  do
    leaf=${file:t}
    name=${file#$base_dir/}

    case $leaf in
      $~remove_pattern) # Temporary files to be removed (eg. backups)
        remove_files=($remove_files $file)
	;;

      $~ignore_pattern) # Files to be ignored
	;;

      *) # Other files. Catch them if no corresponding RCS file
        if [[ ! -f $dir/RCS/$leaf,v ]]
	then
	  echo "NEW      $name"
	  non_vc_files=($non_vc_files $file)
	fi
	;;
    esac
  done

  # Check RCS files
  for file in $dir/RCS/*,v(.N)
  do
    leaf=${${file:t}%,v}
    file=$dir/$leaf
    name=${file#$base_dir/}

    # This hoop because test -w always returns true for root
    writeable=($file(wN))
    if [[ -n $writeable ]]
    then
      # Writable. Either edited or not (presumed locked if writable)
      if rcsdiff -q $file >/dev/null
      then
	echo "LOCKED   $name"
	unchanged_files=($unchanged_files $file)
      else
	echo "MODIFIED $name"
	changed_files=($changed_files $file)
      fi
    else
      # Not writable. Ensure up to date with latest from version history
      if rlog -rdeleted $file >/dev/null 2>&1
      then
        echo "Deleted  ${file#$base_dir/}"
	# May also delete related derived (eg. compiled) files
	rm -f $file ${(e)remove_derived}
      else
	if rcsdiff -q $file >/dev/null
	then
	  echo "Current  ${file#$base_dir/}"
	else
	  echo "Updated  ${file#$base_dir/}"
	  co -u $file 2>/dev/null
	fi
      fi
    fi
  done
done


# List the various types of files and give optional actions for them

if [[ -n $remove_files ]]
then
  echo
  echo ">>> Delete these temporary/backup files?"
  print -c - ${remove_files#$base_dir/}
  if read -q
  then
    rm -f $remove_files
  fi
fi

if [[ -n $non_vc_files ]]
then
  echo
  echo ">>> These files are not version controlled. Add them?"
  print -c - ${non_vc_files#$base_dir/}
  if read -q
  then
    ci -u -t-"$file_desc" $non_vc_files
  fi
fi

if [[ -n $unchanged_files ]]
then
  echo
  echo ">>> These files are checked out but unchanged. Revert?"
  print -c - ${unchanged_files#$base_dir/}
  if read -q
  then
    ci -u $unchanged_files
  fi
fi

if [[ -n $changed_files ]]
then
  echo
  echo ">>> These files are checked out and edited. Check in?"
  print -c - ${changed_files#$base_dir/}
  if read -q
  then
    for file in $changed_files
    do
      rcsdiff -u $file
      ci -u $file
    done
  fi
fi

# Execute a compile command if supplied
if (( ! opts[no-compile] && $#compile_command ))
then
  echo
  echo "Compiling directory $base_dir..."
  eval $compile_command
fi
